source = { script | program }

script = { SOI ~ statements ~ EOI }

program = { SOI ~ NEWLINE* ~ entry_definition ~ NEWLINE+ ~ function_declarations ~ EOI}
entry_definition = ${ SOI ~ "entry" ~ WHITE_SPACE* ~ ":" ~ WHITESPACE* ~ symbol_ref }

function_declarations = _{ function_declaration ~ (NEWLINE+ ~ function_declaration )* ~ NEWLINE*}
function_declaration = { FUNCTION_KEYWORD ~ symbol_ref ~ fn_args ~ NEWLINE? ~ function_body }
function_body = { "{" ~ statements ~ "}" }

fn_args = { "(" ~ ( fn_arg ~ ("," ~ fn_arg )* ~ ","? )? ~ ")" }
fn_arg = _{ expression }

statements = _{ NEWLINE* ~ (statement ~ NEWLINE+)* ~ statement? }

statement = _{ assignment | function_call }
function_call = { symbol_ref ~ fn_args }
assignment = { symbol_ref ~ "=" ~ expression }

expression = _{ function_call | symbol_ref | value  }
symbol_ref = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC|"_")* }
value = _{ string_literal }

string_literal = ${ "\"" ~ string_value ~ "\"" }
string_value = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

FUNCTION_KEYWORD = _{ ("function" | "func" | "fun" | "fn") }

WHITESPACE = _{" " | "\t"}